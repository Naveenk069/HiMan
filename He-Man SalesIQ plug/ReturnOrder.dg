response = Map();
orderid = "";
refundqty = 1;
reason = "";
refundAmount = "";
if(session.containsKey("orderid"))
{
	orderid = session.get("orderid").get("value");
}
if(session.containsKey("refundqty"))
{
	refundqty = session.get("refundqty").get("value").toLong();
}
if(session.containsKey("reason"))
{
	reason = session.get("reason").get("value");
}
if(session.containsKey("refundAmount"))
{
	refundAmount = session.get("refundAmount").get("value");
}
if(orderid == "")
{
	response.put("status","failed");
	response.put("message","Missing Order ID");
	return response;
}
order_details = invokeurl
[
	url :"https://<your-shop-name>.myshopify.com/admin/api/2025-10/orders/" + orderid + ".json"
	type :GET
	headers:{"Content-Type":"application/json"}
	connection:"<your_connection_name>"
];
info "ORDER_DETAILS → " + order_details;
line_items = order_details.get("order").get("line_items");
lineitemid = line_items.get(0).get("id");
info "LINE_ITEM_ID → " + lineitemid;
transactions_json = "\"kind\":\"refund\",";
if(refundAmount != "")
{
	transactions_json = transactions_json + "\"amount\":\"" + refundAmount + "\"," + "\"currency\":\"INR\",";
}
json_body = "{ \"refund\": {" + "\"note\":\"Return Reason: " + reason + "\"," + "\"refund_line_items\": [" + "  { \"line_item_id\": " + lineitemid + ", \"quantity\": " + refundqty + " }" + "]," + "\"transactions\": [ {" + transactions_json + "\"status\":\"success\"" + "} ]" + "} }";
// Debug
info "REFUND_JSON → " + json_body;
refund_resp = invokeurl
[
	url :"https://<your-shop-name>.myshopify.com/admin/api/2025-10/orders/" + orderid + "/refunds.json"
	type :POST
	body:json_body
	headers:{"Content-Type":"application/json"}
	connection:"<your_connection_name>"
];
info "RAW_REFUND_RESPONSE → " + refund_resp;
if(refund_resp.containsKey("refund"))
{
	refundid = refund_resp.get("refund").get("id").toString();
	response.put("refundid",refundid);
	response.put("status","success");
	response.put("message","Refund processed successfully");
}
else
{
	response.put("status","failed");
	response.put("message","Refund API error: " + refund_resp.toString());
}
return response;